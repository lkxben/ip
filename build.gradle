plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'checkstyle'
}

repositories {
    mavenCentral()
}

// Detect OS for JavaFX classifier
def osName = System.getProperty("os.name").toLowerCase()
def osClassifier = "linux" // default
if (osName.contains("win")) {
    osClassifier = "win"
} else if (osName.contains("mac")) {
    osClassifier = "mac"
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.0'

    String javaFxVersion = '17.0.7'

    implementation "org.openjfx:javafx-base:$javaFxVersion:$osClassifier"
    implementation "org.openjfx:javafx-controls:$javaFxVersion:$osClassifier"
    implementation "org.openjfx:javafx-fxml:$javaFxVersion:$osClassifier"
    implementation "org.openjfx:javafx-graphics:$javaFxVersion:$osClassifier"
}

application {
    mainClass.set("cate.ui.Main")
}

shadowJar {
    archiveBaseName = "cate"
    archiveClassifier = null
    archiveFileName = 'cate.jar'
}

checkstyle {
    toolVersion = '11.0.0'
}

run {
    standardInput = System.in
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true
        showStandardStreams = false
    }
}